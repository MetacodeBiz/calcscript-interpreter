apply plugin: 'java-library'
apply plugin: 'findbugs'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'biz.metacode.calcscript'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'biz.metacode.calcscript.interpreter.shell.Main'
    }
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version:'2.0.0'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'interpreter'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Calcscript interpreter'
                description = 'Calcscript is a stack based language designed with the purpose of executing complex calculations using minimum number of keystrokes'
                url = 'https://github.com/MetacodeBiz/calcscript-interpreter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'wiktor'
                        name = 'Wiktor Kwapisiewicz'
                        email = 'wiktor@metacode.biz'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/MetacodeBiz/calcscript-interpreter'
                    developerConnection = 'scm:git:https://github.com/MetacodeBiz/calcscript-interpreter'
                    url = 'https://github.com/MetacodeBiz/calcscript-interpreter'
                }
            }
        }
    }
    repositories {
        maven {
            name 'sonatypeStaging'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

defaultTasks 'clean', 'test', 'findbugsMain', 'findbugsTest', 'jar'
